// *****
// * Original by Xyio
// * Modified by Hanebu
// *****

// ****************************
// * ↓↓↓ ADD OWN MOVEMENT ↓↓↓ *
while not position 1748 1677
    walk 'West'
endwhile
walk 'North'
walk 'North'
pause 600
// * ↑↑↑ ADD OWN MOVEMENT ↑↑↑ *
// ****************************

// ****************************************
// * ↓↓↓ DO NOT MODIFY THE CODE BELOW ↓↓↓ *
// ****************************************

// Welcome message
overhead "::: bank controller :::"
clearsysmsg 

// Create temp pacly list to clear other lists and write values back
createlist 'tempPackieList'

if not listexists emptyPackieList or not listexists fullPackieList
    script 'Miner_Settings'
endif

// Set packy dumps back to zero
setvar! 'packyDumps' 0

// Smelt all ore that is in your own backpack
while findtype 'iron ore' backpack as 'ore'
    dclick 'ore'
    wait 600
endwhile

// Smelt all ore from full packies 
while list 'fullPackieList' > 0 
    if atlist 'fullPackieList' 0 as 'thePack'
        // Open first packie in the list  
        dclick 'thePack'
        wait 600
        // Check for ore and smelt it
        while findtype 'iron ore' 'thePack' as 'ore'
            overhead "::: smelting from packie :::"
            dclick 'ore'
            wait 600
        endwhile
    endif
    // Remove packie from full list and put it to temp list
    if atlist 'fullPackieList' 0 as 'packie'
        poplist 'fullPackieList' 'front'
        pushlist 'tempPackieList' 'packie' 'back'
    endif
endwhile

// Smelt all ore from partially full packies 
while list 'emptyPackieList' > 0 
    if atlist 'emptyPackieList' 0 as 'thePack'
        // Open first packie in the list  
        dclick 'thePack'
        wait 600
        // Check for ore and smelt it
        while findtype 'iron ore' 'thePack' as 'ore'
            overhead "::: smelting from Packie :::"
            dclick 'ore'
            wait 600
        endwhile
    endif
    // Remove packie from partially list and put it to temp list
    if atlist 'emptyPackieList' 0 as 'packie'
        poplist 'emptyPackieList' 'front'
        pushlist 'tempPackieList' 'packie' 'back'
    endif    
endwhile
    
// Clear temp list and add all packies to empty list
while list 'tempPackieList' > 0
    if atlist 'tempPackieList' 0 as 'packie'
        poplist 'tempPackieList' 'front'
        pushlist 'emptyPackieList' 'packie' 'back'       
    endif
endwhile

// Dump ingots into Resource Stockpile
if findtype 23034 'ground' -1 1 3 as 'stockpile'
    menu 'stockpile' 0
    waitfortarget 
    target 'self'
endif

// Next recall spot
script 'M_RecallController'
